import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

kotlin {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_1_8)
    }
}

android {
    compileSdk = 35

    defaultConfig {
        applicationId "io.keepalive.android"
        minSdk 22
        targetSdk 35
        versionCode 133
        versionName "1.3.3-lite"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'

        // by default include the webhook functionality
        buildConfigField "boolean", "INCLUDE_WEBHOOK", "true"
    }

    // add a debug manifest
    sourceSets {
        debug {
            manifest.srcFile 'src/debug/AndroidManifest.xml'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            // Append an identifier to the applicationId when debugging
            applicationIdSuffix ".debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding = true
        buildConfig = true
    }
    namespace = 'io.keepalive.android'

    flavorDimensions += "platform"
    productFlavors {
        // we don't need to define anything else
        create("googlePlay") {}

        // only disable metadata for f-droid builds
        create("fDroid") {
            dependenciesInfo {
                // Disables dependency metadata when building APKs.
                includeInApk = false
                // Disables dependency metadata when building Android App Bundles.
                includeInBundle = false
            }
        }

        create("fDroidLite") {

            // disable the webhook functionality
            buildConfigField "boolean", "INCLUDE_WEBHOOK", "false"

            // use a different application id so fDroid knows it is a different app
            applicationId = "io.keepalive.lite.android"

            dependenciesInfo {
                // Disables dependency metadata when building APKs.
                includeInApk = false
                // Disables dependency metadata when building Android App Bundles.
                includeInBundle = false
            }
        }
    }

    // fDroidLite uses the same platform-specific code as fDroid (like LocationHelper)
    sourceSets.named("fDroidLite") {
        java.srcDirs("src/fDroid/java")
    }
}

dependencies {

    implementation 'com.squareup.okhttp3:okhttp:5.1.0'

    // next versions require compile sdk 36
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.activity:activity-ktx:1.10.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.9.5'

    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'androidx.preference:preference-ktx:1.2.1'

    // for encrypted shared preferences, no longer used
    // implementation 'androidx.security:security-crypto:1.1.0-alpha06'

    // work manager stuff, but also gives us ListenableFuture...
    implementation 'androidx.work:work-runtime-ktx:2.10.5'

    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    // this is what the docs say to use for ListenableFuture but
    //  there was so much extra code...
    //implementation 'com.google.guava:guava:31.0.1-android'

    // if this is google play, and not f-droid, then use the google play location library
    googlePlayImplementation 'com.google.android.gms:play-services-location:21.3.0'

    // test stuff
    testImplementation 'org.mockito:mockito-core:5.20.0'
    testImplementation 'junit:junit:4.13.2'

    // android instrumented test dependencies
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.7.0'
    androidTestImplementation 'org.mockito:mockito-android:5.20.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.7.0'
    androidTestImplementation 'tools.fastlane:screengrab:2.1.1'
    //androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.3.0'
}